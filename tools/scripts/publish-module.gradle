apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  if (project.plugins.findPlugin("com.android.library")) {
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
  } else {
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
  }
}

artifacts {
  archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION


afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        groupId PUBLISH_GROUP_ID
        artifactId PUBLISH_ARTIFACT_ID
        version PUBLISH_VERSION
        if (project.plugins.findPlugin("com.android.library")) {
          from components.release
        } else {
          from components.java
        }

        artifact androidSourcesJar

        pom {
          name = PUBLISH_ARTIFACT_ID
          description = 'Osmon is a modern assertion library for Kotlin'
          url = 'https://github.com/behzodhalil/osmon'
          licenses {
            license {
              name = 'Osmon License'
            }
          }
          developers {
            developer {
              id = 'behzod'
              name = 'Behzod'
              email = 'behzoddev@gmail.com'
            }
          }

          scm {
            connection = 'scm:git:github.com/behzodhalil/osmon.git'
            developerConnection = 'scm:git:ssh://github.com/behzodhalil/osmon.git'
            url = 'https://github.com/behzodhalil/osmon'
          }
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(
    rootProject.ext["signing.keyId"],
    rootProject.ext["signing.key"],
    rootProject.ext["signing.password"],
  )
  sign publishing.publications
}
